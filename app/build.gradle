apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'com.abhi.minibrainage'
    compileSdk 35
    ndkVersion "28.1.13356709"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    defaultConfig {
        applicationId "com.abhi.minibrainage"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 13
        versionName "1.2.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // Deobfuscation file located under:
            // app/build/outputs/mapping/release/mapping.txt
            // AAB: BUNDLE-METADATA/com.android.tools.build.obfuscation/proguard.map
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Native debug symbols located under:
            // app/build/outputs/native-debug-symbols/variant-name/native-debug-symbols.zip
            // AAB: BUNDLE-METADATA/com.android.tools.build.debugsymbols
            // Or follow: https://stackoverflow.com/a/68778908
            ndk {
                debugSymbolLevel "FULL"
            }
        }
    }

    buildFeatures {
        viewBinding true
        mlModelBinding true
        buildConfig true
    }

    androidResources {
        noCompress = 'tflite'
    }
}

dependencies {
    def googlePlayVersion = '2.0.2'

    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation 'com.google.android.gms:play-services-games-v2:20.1.2'
    implementation 'org.tensorflow:tensorflow-lite:2.16.1'
    implementation "com.google.android.play:review:$googlePlayVersion"
    implementation "com.google.android.play:review-ktx:$googlePlayVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}